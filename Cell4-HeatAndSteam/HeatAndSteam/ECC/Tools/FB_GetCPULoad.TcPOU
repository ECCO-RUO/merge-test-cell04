<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.5">
  <POU Name="FB_GetCPULoad" Id="{70ee7357-34f0-4162-9fda-b8bdd33a860a}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_GetCPULoad
VAR_INPUT
	rInterval			: REAL := 10.0;		// [sec]
END_VAR
VAR_OUTPUT
	rCPULoad			: REAL;				// [%]
	rCPUFrequency		: REAL;
	bError				: BOOL;				// error flag (indicator: error occured)
	nErrID				: UDINT;			// last error ID 
END_VAR
VAR
	// internal use
	sAmsNetId			: STRING := '';		// ADS Net ID (local = '')    
	iState				: INT;
	uData				: UINT;
    stMDP_ModuleHeader  : ST_MDP_ModuleHeader;
    stMDP_ModuleContent : ST_MDP_CPU;
	// FB instances
	fbReadMDPCPU		: Tc2_MDP.FB_MDP_CPU_Read;
	tonInterval			: Tc2_Standard.TON;
	// Info
	iErrorCnt			: UINT;
	iGoodCnt			: UINT;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[
// Interval timer
tonInterval(IN := NOT tonInterval.Q, PT := REAL_TO_TIME(rInterval * 1000.0));

CASE iState OF
	0: // Initiate program parameters (if required)
		fbReadMDPCPU(bExecute := FALSE);
		IF (tonInterval.Q) THEN
			iState := 10;
		END_IF

	10:	// Trigger FB: request CPU data
		fbReadMDPCPU(	bExecute := TRUE,
						tTimeout := T#5S,
						iModIdx := 0,
						sAmsNetId := sAmsNetId,
						stMDP_ModuleHeader => stMDP_ModuleHeader,
						stMDP_ModuleContent => stMDP_ModuleContent 	);
		iState := 20;

	20:	// Process FB: Request CPU temp
		fbReadMDPCPU(bExecute := FALSE); 
		IF (NOT fbReadMDPCPU.bBusy) THEN
			IF (fbReadMDPCPU.bError) THEN
				bError := TRUE;
				nErrID := fbReadMDPCPU.nErrID; 
				iErrorCnt := iErrorCnt + 1;
				iState := 0;
			ELSE
				bError := FALSE;
				rCPULoad := UINT_TO_REAL(stMDP_ModuleContent.iCPUusage);
				rCPUFrequency := UDINT_TO_REAL(stMDP_ModuleContent.iCPUfrequency);
				iGoodCnt := iGoodCnt + 1;
				iState := 30;
			END_IF                                               
		END_IF
    
     30: // Done 
		iState := 0;

	ELSE
		iState := 0; 

END_CASE
]]></ST>
    </Implementation>
    <LineIds Name="FB_GetCPULoad">
      <LineId Id="94" Count="0" />
      <LineId Id="93" Count="0" />
      <LineId Id="17" Count="0" />
      <LineId Id="77" Count="0" />
      <LineId Id="18" Count="2" />
      <LineId Id="82" Count="0" />
      <LineId Id="21" Count="0" />
      <LineId Id="83" Count="0" />
      <LineId Id="22" Count="2" />
      <LineId Id="114" Count="0" />
      <LineId Id="90" Count="0" />
      <LineId Id="71" Count="0" />
      <LineId Id="117" Count="1" />
      <LineId Id="29" Count="3" />
      <LineId Id="34" Count="3" />
      <LineId Id="125" Count="0" />
      <LineId Id="38" Count="3" />
      <LineId Id="120" Count="0" />
      <LineId Id="124" Count="0" />
      <LineId Id="43" Count="4" />
      <LineId Id="49" Count="0" />
      <LineId Id="73" Count="0" />
      <LineId Id="51" Count="1" />
      <LineId Id="72" Count="0" />
      <LineId Id="11" Count="0" />
      <LineId Id="75" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>